buildscript {
    ext.kotlin_version = "1.2.21"
    ext.junit_version = "5.0.3"

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
    }
}

group "org.celtric.kotlin"
version "0.0.1"

apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
}

compileKotlin     { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.property("bintray.user") ?: null
    key = project.property("bintray.apikey") ?: null
    publications = ["mavenPublication"]

    pkg {
        repo = "maven"
        name = "kotlin-html"

        version {
            attributes = ["gradle-plugin": "com.use.less:com.use.less.gradle:gradle-useless-plugin"]
        }
    }
}
